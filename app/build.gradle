plugins {
    id "com.android.application"
    id "checkstyle"
}

import org.dicio.sentences_compiler.compiler.CompilerBase
import org.dicio.sentences_compiler.main.SentencesCompiler
import org.dicio.sentences_compiler.compiler.CompilerToJava
import org.dicio.sentences_compiler.util.CompilerError

import java.nio.charset.StandardCharsets

android {
    compileSdkVersion 31
    namespace 'org.stypox.dicio'

    defaultConfig {
        applicationId "org.stypox.dicio"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 8
        versionName "0.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        // add folders generated by sentencesCompiler task
        sourceSets.main.java.srcDirs += "build/generated/source/sentences/main"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }
}

ext {
    checkstyleVersion = '10.0'
}

configurations {
    checkstyle
}

checkstyle {
    getConfigDirectory().set(rootProject.file("checkstyle"))
    ignoreFailures false
    showViolations true
    toolVersion = checkstyleVersion
}

task runCheckstyle(type: Checkstyle) {
    source 'src/main'
    include '**/*.java'
    classpath = configurations.checkstyle
    showViolations true
}

afterEvaluate {
    preDebugBuild.dependsOn runCheckstyle
}

dependencies {
    // Desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Checkstyle
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"

    // Android
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference:1.2.0'

    // RxJava3
    implementation "io.reactivex.rxjava3:rxjava:3.0.13"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"

    // Dicio own libraries
    implementation 'com.github.Stypox:dicio-skill:050d181c942f8ac7ef0bef5fbc2c049bba6fa127'
    implementation 'com.github.Stypox:dicio-numbers:03e71abbe0259e1782ef5e7f5d00f7ac8cf2cc1e'

    // Vosk
    implementation 'net.java.dev.jna:jna:5.8.0@aar'
    implementation 'com.alphacephei:vosk-android:0.3.32'

    // Miscellaneous
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'org.unbescape:unbescape:1.1.6.RELEASE'
    implementation 'org.jsoup:jsoup:1.14.3'

    // Used by skills
    implementation 'net.objecthunter:exp4j:0.4.8'

    // Testing
    testImplementation 'junit:junit:4.13.2'
}


task sentencesCompiler {
    doFirst {
        File baseInputDirectory = file("src/main/sentences/")
        File outputDirectory = file("build/generated/source/sentences/main/org/stypox/dicio")
        outputDirectory.mkdirs()

        List<String> locales = new ArrayList<>()
        Set<String> allSectionIds = new HashSet<>()
        for (String localeName :
                baseInputDirectory.list({ dir, name -> new File(dir, name).isDirectory() })) {

            List<String> inputFiles = new ArrayList<>()
            for (File file : new File(baseInputDirectory, localeName).listFiles()) {
                try {
                    // first try to compile each file separately: if it fails, exclude it
                    OutputStreamWriter unusedOutput = new OutputStreamWriter(
                            new ByteArrayOutputStream(), StandardCharsets.UTF_8)
                    String fileString = "UTF-8:${file.absolutePath}"
                    SentencesCompiler.compile(Collections.singletonList(fileString),
                            unusedOutput, unusedOutput, new CompilerBase() {})
                    inputFiles.add(fileString)

                } catch (CompilerError e) {
                    System.err.println("[ERROR] Ignoring invalid ${localeName} sentences file: "
                            + e.getMessage())
                }
            }

            if (inputFiles.isEmpty()) {
                continue
            }
            // there is surely at least one file for this locale: we can add it
            locales.add(localeName)

            OutputStream outputStream = new FileOutputStream(file("${outputDirectory}/Sentences_${localeName}.java"))
            ByteArrayOutputStream sectionIdsStream = new ByteArrayOutputStream()
            SentencesCompiler.compile(
                    inputFiles,
                    new OutputStreamWriter(outputStream, StandardCharsets.UTF_8),
                    new OutputStreamWriter(sectionIdsStream, StandardCharsets.UTF_8),
                    new CompilerToJava("", "org.stypox.dicio", "Sentences_${localeName}", "sections"))
            outputStream.close()
            sectionIdsStream.close()

            String sectionIds = new String(sectionIdsStream.toByteArray(), StandardCharsets.UTF_8)
            allSectionIds.addAll(sectionIds.split(" "))
        }


        OutputStream fileOutputStream = new FileOutputStream(file("${outputDirectory}/SectionsGenerated.java"))
        OutputStreamWriter outputStream = new OutputStreamWriter(fileOutputStream, StandardCharsets.UTF_8)
        outputStream.write("/*\n"
                + " * FILE AUTO-GENERATED BY GRADLE TASK sentencesCompiler.\n"
                + " * DO NOT MODIFY THIS FILE, AS EDITS WILL BE OVERWRITTEN.\n"
                + " */\n"
                + "\n"
                + "package org.stypox.dicio;\n"
                + "import org.dicio.skill.standard.StandardRecognizerData;\n"
                + "import java.util.HashMap;\n"
                + "import java.util.Map;\n")

        outputStream.write("public class SectionsGenerated {\n" +
                "public static final Map<String,Map<String,StandardRecognizerData>> localeSectionsMap=new HashMap<String,Map<String,StandardRecognizerData>>(){{")
        for (String localeName : locales) {
            outputStream.write("put(\"${localeName.toLowerCase()}\",Sentences_${localeName}.sections);")
        }
        outputStream.write("}};\n")

        if (!allSectionIds.isEmpty()) {
            outputStream.write("public static final String ")

            boolean first = true
            for(String sectionId : allSectionIds) {
                if (first) {
                    first = false
                } else {
                    outputStream.write(",")
                }
                outputStream.write("${sectionId}=\"${sectionId}\"")
            }
            outputStream.write(";\n")
        }
        outputStream.write("}\n")
        outputStream.flush()
        fileOutputStream.close()
    }
}
preBuild.dependsOn(sentencesCompiler)
